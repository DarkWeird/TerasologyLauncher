/*
 * Copyright 2019 MovingBlocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Uses Bellsoft Liberica JRE
def jreVersion = '8u212'
def jreUrlBase = "https://download.bell-sw.com/java/$jreVersion/bellsoft-jre$jreVersion"
def jreUrlFilenames = [
        Linux64   : 'linux-amd64.tar.gz',
        Linux32   : 'linux-i586.tar.gz',
        Windows64 : 'windows-amd64.zip',
        Windows32 : 'windows-i586.zip',
        Mac       : 'macos-amd64.zip'
]

task downloadJreAll {
    group 'Download'
    description 'Downloads JRE for all platforms'
}

jreUrlFilenames.each { os, file ->
    def jreTask = "downloadJre$os"

    task "$jreTask"(type: Download) {
        group 'Download'
        description "Downloads JRE for $os"

        src "$jreUrlBase-$file"
        dest "$projectDir/jre/$file"
        overwrite false
    }

    downloadJreAll.dependsOn tasks.named(jreTask).get()
}
