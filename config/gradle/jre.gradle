/*
 * Copyright 2020 MovingBlocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Uses Bellsoft Liberica JRE
def jreVersionGame = '8u212'
def jdkVersionLauncher = '11.0.6+10' // jre version haven't jmod (needed for jlink/jpackager)

def jreUrlFilenames = [
        Linux64  : 'linux-amd64-full.tar.gz',
        Windows64: 'windows-amd64-full.zip',
        Windows32: 'windows-i586-full.zip',
        Mac      : 'macos-amd64-full.zip'
]

task createRelease() {
    group 'Distribution'
    description 'Bundles the project with a JRE for each platform'
    dependsOn distZip

    doLast {
        println 'Created release: ' + displayVersion
    }
}

task downloadJre {
    group 'Download'
    description 'Downloads Launcher JRE for all platforms'
}


def createJreTasks(String taskNameBase,
                   String downloadUrl,
                   String downloadFile,
                   String unpackDir) {

    task "download$taskNameBase"(type: Download) {
        group 'Download'
        src downloadUrl
        dest downloadFile
        overwrite false
    }

    task "unpack$taskNameBase"(type: Copy) {
        from(downloadFile.endsWith("zip")
                ? zipTree(downloadFile)
                : tarTree(downloadFile)) {
            eachFile { fcd ->
                fcd.relativePath = new RelativePath(
                        true, fcd.relativePath.segments.drop(1))
            }
            includeEmptyDirs = false
        }
        into unpackDir

        outputs.dir unpackDir

        dependsOn "download${taskNameBase}"
    }
}

jreUrlFilenames.each { os, file ->
    // JDK 11
    def launcherTaskBase = "Jre${os}"
    createJreTasks(
            launcherTaskBase,
            "https://download.bell-sw.com/java/$jdkVersionLauncher/bellsoft-jdk$jdkVersionLauncher-$file",
            "$projectDir/jre/$os-$jdkVersionLauncher-$file",
            "$projectDir/jre/$os")
    downloadJre.dependsOn "unpack${launcherTaskBase}"
    def distName = os.toLowerCase()
    
    distributions {
        "$distName" {
            contents {
                into ('jre') {
                    from "$projectDir/jre/$os"
                }
                with distributions.main.contents
            }
        }
    }

    def zipTask = tasks.named("${distName}DistZip").get()
    def tarTask = tasks.named("${distName}DistTar").get()

    zipTask.dependsOn downloadJre
    zipTask.description "Bundles the project with a JRE for ${os}"
    tarTask.dependsOn downloadJre
    tarTask.description "Bundles the project with a JRE for ${os}"
    createRelease.dependsOn zipTask
}

/**
 * Modify start scripts to use bundled JRE when
 * available, otherwise fall back to searching.
 */
startScripts.doLast {
    unixScript.text = unixScript
            .text
            .replaceFirst(/(?s)# Determine the Java.*(?=\R\R# Increase)/) { match ->
                """\
                |# Use bundled JRE when available
                |if [ -d \$APP_HOME/jre ] ; then
                |    JAVACMD=\$APP_HOME/jre/bin/java
                |else
                |    ${match.replace("\n", "\n    ")}
                |fi"""
                .stripMargin()
            }

    windowsScript.text = windowsScript
            .text
            .replaceFirst(/(?s)@rem Find java.*:init/) { match ->
                """\
                |@rem Use bundled JRE when available
                |if not exist %APP_HOME%\\jre\\ goto findJava
                |set JAVA_EXE=%APP_HOME%\\jre\\bin\\java.exe
                |goto init
                |
                |:findJava
                |$match
                |"""
                .stripMargin()
            }
}
